module expr

imports

  signatures/start-sig

  base
  env
  sec

rules

  typeOfExpr: Env * Expr -> LTYPE
  typeOfBinOp : BinOp * TYPE * TYPE -> TYPE

  // generic rule for binary operations
  typeOfExpr(ENV, BinExpr(e1, op, e2)) = (T, S) :- {S1 S2 T1 T2}
    typeOfExpr(ENV, e1) == (T1, S1),
    typeOfExpr(ENV, e2) == (T2, S2),
    typeOfBinOp(op, T1, T2) == T,
    or(S1, S2) == S.

rules // Boolean Expressions

  typeOfExpr(_, Literal(True()))    = (BOOL(), LOW()).
  typeOfExpr(_, Literal(False()))   = (BOOL(), LOW()).

  typeOfExpr(ENV, UnExpr(Neg(), e)) = LT :-
    typeOfExpr(ENV, e) == LT@(BOOL(), _).


  typeOfBinOp(And(), BOOL(), BOOL()) = BOOL().
  typeOfBinOp(Or() , BOOL(), BOOL()) = BOOL().
  typeOfBinOp(Eq() , BOOL(), BOOL()) = BOOL().

rules  // Numerical Expressions

  typeOfExpr(_, Literal(Number(_))) = (INT(), LOW()).

  typeOfExpr(ENV, UnExpr(Min(), e)) = (INT(), S) :-
    typeOfExpr(ENV, e) == (INT(), S).


  // Arithmetic Operations
  typeOfBinOp(Plus()  , INT(), INT()) = INT().
  typeOfBinOp(Minus() , INT(), INT()) = INT().
  typeOfBinOp(Divide(), INT(), INT()) = INT().
  typeOfBinOp(Times() , INT(), INT()) = INT().


  // Comparison Operations
  typeOfBinOp(Eq(), INT(), INT()) = BOOL().
  typeOfBinOp(Lt(), INT(), INT()) = BOOL().
  typeOfBinOp(Gt(), INT(), INT()) = BOOL().

rules // Identifiers

  typeOfExpr(ENV, Ident(x)) = T :-
    get(ENV, x) == T
    | error $[Cannot find variable [x] (or it has incompatible security label).].

rules // Array length

  typeOfExpr(ENV, Arlen(x)) = (INT(), S) :-
    get(ENV, x) == (ARRAY(_), S).
   